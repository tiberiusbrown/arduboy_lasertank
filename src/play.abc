import input;
import level;
import render;

void play()
{
    if(tiles[tanky * 16 + tankx] == T_FLAG)
    {
        render_level();
        constexpr u8 W = 50;
        constexpr u8 H = 15;
        constexpr u8 X = 64 - W / 2;
        constexpr u8 Y = 32 - H / 2;
        $draw_filled_rect(X, Y, W, H, BLACK);
        $draw_rect(X + 1, Y + 1, W - 2, H - 2, WHITE);
        $set_text_font(FONT_PIXELOIDSANSBOLD);
        $draw_text(42, 35, "Victory!");
        return;
    }

    if(!$pressed(B_BUTTON))
    {
        if(btn_just_pressed(UP_BUTTON))
            process_tank_move(DIR_U, 0, -1);
        if(btn_just_pressed(RIGHT_BUTTON))
            process_tank_move(DIR_R, 1, 0);
        if(btn_just_pressed(DOWN_BUTTON))
            process_tank_move(DIR_D, 0, 1);
        if(btn_just_pressed(LEFT_BUTTON))
            process_tank_move(DIR_L, -1, 0);
    }

    if($just_pressed(B_BUTTON))
    {
        can_pan = true;
        b_pressed_time = $millis();
    }

    if($just_released(B_BUTTON) && $millis() < b_pressed_time + PAUSE_PRESS_MS)
    {
        state = STATE_PAUSE;
    }

    if(can_pan && $pressed(B_BUTTON))
    {
        constexpr i8 PAN_SPEED = 3;
        if($pressed(UP_BUTTON  )) targety -= PAN_SPEED;
        if($pressed(DOWN_BUTTON)) targety += PAN_SPEED;
    }
    else
        adjust_to_tank();

    if(targety <  0) targety =  0;
    if(targety > 64) targety = 64;

    if(offy < targety) offy += (targety - offy + 3) / 4;
    if(offy > targety) offy -= (offy - targety + 3) / 4;

    render_level();

    if(can_pan && $pressed(B_BUTTON))
    {
        $draw_filled_rect(125, 0, 3, 64, BLACK);
        $draw_filled_rect(126, offy / 2, 2, 32, WHITE);
    }
}

void process_tank_move(u8 dir, i8 dx, i8 dy)
{
    if(tankdir != dir)
    {
        tankdir = dir;
        return;
    }

    u8 nx = tankx + dx;
    u8 ny = tanky + dy;
    if(nx >= 16 || ny >= 16)
        return;

    u8 ni = ny * 16 + nx;

    if(tiles[ni] >= T_FIRST_BLOCKING)
        return;

    tankx = nx;
    tanky = ny;
}
