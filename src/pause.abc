import vars;

import hint;

char[] prog&[2] SIZE_STRS = { "Tiny", "Normal" };
char[] prog&[2] ANIM_STRS = { "Off", "On" };
char[] prog&[4] SPEED_STRS = { "Slow", "Normal", "Fast", "Insane" };

u8[4] prog SPEEDS = { 5, 3, 2, 1 };

u8 pause_i;

void pause()
{
    if(pause_i > 0 && btn_just_pressed(UP_BUTTON))
        pause_i -= 1;
    if(pause_i < 4 && btn_just_pressed(DOWN_BUTTON))
        pause_i += 1;

    u8 speed;
    switch(anim_frames)
    {
        case(1) speed = 3;
        case(2) speed = 2;
        case(3) speed = 1;
        case(5) speed = 0;
    }

    if($just_pressed(A_BUTTON))
    {
        switch(pause_i)
        {
            case(1) state = STATE_HINT;
            case(2) if(++tilesize > TILESIZE_8) tilesize = TILESIZE_4;
            case(3) tile_anim = !tile_anim;
            case(4) if(++speed > 3) speed = 0;
        }
    }

    render_level();
    $draw_filled_rect(64, 0, 64, 64, BLACK);

    $draw_vline(64, 0, 64, WHITE);

    $set_text_font(FONT_BR5D);

    $draw_text(85, 5, "Paused");
    $draw_hline(66, 6, 62, WHITE);

    constexpr u8 X2 = 104;

    $draw_text(75, 15, "Undo");
    $draw_text(75, 22, "Hint");
    $draw_text(75, 29, "Tiles:"); $draw_text(X2, 29, SIZE_STRS[tilesize]);
    $draw_text(75, 36, "Anim:");  $draw_text(X2, 36, ANIM_STRS[tile_anim]);
    $draw_text(75, 43, "Speed:"); $draw_text(X2, 43, SPEED_STRS[speed]);

    anim_frames = SPEEDS[speed];

    $draw_text(68, pause_i * 7 + 15, ">");

    if($just_pressed(B_BUTTON))
    {
        state = STATE_PLAY;
        can_pan = false;
    }
}
