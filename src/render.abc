void render_level()
{
    u8 off = (frame >> 2) & 3;

    // temporarily remove tank
    u8& t = objects[tanky * 16 + tankx];
    t = T_DIRT;

    for(u8 r = 0; r < 16; ++r)
    {
        i16 y = r * 8 - offy;
        if(y <= -8) continue;
        if(y >= 64) break;
        u8[16] row = tiles[r*16 +: 16];
        u8[16] row2 = objects[r*16 +: 16];
        for(u8 c = 0; c < 16; ++c)
        {
            $draw_sprite(c * 8, y, TILES, row[c] + off);
            u8 f = row2[c];
            if(f != 0)
                $draw_sprite(c * 8, y, TILES, f + off);
        }
    }

    t = T_TANK;

    $draw_sprite(tankx * 8, tanky * 8 - offy, TILES, T_TANK + tankdir);

    if(moving)
        $draw_sprite(movex * 8, movey * 8 - offy, TILES, moveobj);

    if(shooting)
        $draw_sprite(shootx * 8, shooty * 8 - offy, LASERS, shootdir);
}
