void render_level()
{
    u8 off = (frame >> 2) & 3;
    if(!tile_anim) off = 0;

    if(tilesize == TILESIZE_4)
    {
        for(u8 r = 0; r < 16; ++r)
        {
            i16 y = r * 4;
            u8[16] row = tiles[r*16 +: 16];
            u8[16] row2 = objects[r*16 +: 16];
            for(u8 c = 0; c < 16; ++c)
            {
                u8 f = row[c];
                if(f != 0)
                    $draw_sprite(c * 4, y, TILES4, f + off);
                f = row2[c];
                if(f != 0)
                    $draw_sprite(c * 4, y, TILES4, f + off);
            }
        }

        if(shooting)
        {
            u8 f = shootdir;
            switch(objects[shooty * 16 + shootx])
            {
                case(T_MIRROR + 0x0) f = 4;
                case(T_MIRROR + 0x4) f = 5;
                case(T_MIRROR + 0x8) f = 6;
                case(T_MIRROR + 0xc) f = 7;
            }
            switch(tiles[shooty * 16 + shootx])
            {
                case(T_MIRROR_ROT + 0x0) f = 4;
                case(T_MIRROR_ROT + 0x4) f = 5;
                case(T_MIRROR_ROT + 0x8) f = 6;
                case(T_MIRROR_ROT + 0xc) f = 7;
            }
            $draw_sprite(shootx * 4, shooty * 4, TILES4, 0xd0 + f);
        }
        
        if(!dead && (first_move || (frame & 15) >= 5))
            $draw_sprite(tankx * 4, tanky * 4, TILES4, T_TANK + tankdir);

        if(state != STATE_PAUSE)
        {
            $draw_vline(64, 0, 64, WHITE);

            $set_text_font(FONT_BR5D);
            char[32] buf;
            $format(buf, "Level %u/%u", current_level + 1, len(LEVELSETS[current_set].levels));
            u8 w = $text_width(buf);
            $draw_text(97 - w/2, 5, buf);
            $draw_hline(66, 6, 62, WHITE);
            $draw_text(66, 17, "Moves:");
            $draw_text(69, 28, "Shots:");
            $set_text_font(FONT_PIXELOIDSANS);
            $draw_textf(93, 18, "%u", moves);
            $draw_textf(93, 29, "%u", shots);
        }
    }
    else if(tilesize == TILESIZE_8)
    {
        for(u8 r = 0; r < 16; ++r)
        {
            i16 y = r * 8 - offy;
            if(y <= -8) continue;
            if(y >= 64) break;
            u8[16] row = tiles[r*16 +: 16];
            u8[16] row2 = objects[r*16 +: 16];
            for(u8 c = 0; c < 16; ++c)
            {
                $draw_sprite(c * 8, y, TILES8, row[c] + off);
                u8 f = row2[c];
                if(f != 0)
                    $draw_sprite(c * 8, y, TILES8, f + off);
            }
        }

        if(shooting)
        {
            u8 f = shootdir;
            switch(objects[shooty * 16 + shootx])
            {
                case(T_MIRROR + 0x0) f = 4;
                case(T_MIRROR + 0x4) f = 5;
                case(T_MIRROR + 0x8) f = 6;
                case(T_MIRROR + 0xc) f = 7;
            }
            switch(tiles[shooty * 16 + shootx])
            {
                case(T_MIRROR_ROT + 0x0) f = 4;
                case(T_MIRROR_ROT + 0x4) f = 5;
                case(T_MIRROR_ROT + 0x8) f = 6;
                case(T_MIRROR_ROT + 0xc) f = 7;
            }
            $draw_sprite(shootx * 8, shooty * 8 - offy, LASERS, f);
        }

        if(!dead && (first_move || (frame & 15) >= 5))
            $draw_sprite(tankx * 8, tanky * 8 - offy, TILES8, T_TANK + tankdir);
    }
}
